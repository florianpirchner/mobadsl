/**
 * Copyright (c) 2015 - 2016, Lunifera GmbH (Wien), Ekkehard Gentz (Rosenheim)
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *         Florian Pirchner - Initial implementation
 */
module org.mobadsl.grammar.GenerateMoba

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		registerGeneratedEPackage = "org.mobadsl.semantic.model.moba.MobaPackage"
		registerGenModelFile = "platform:/resource/org.mobadsl.semantic.model/model/Moba.genmodel"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.mobadsl.grammar"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				web = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "org.mobadsl.grammar.Moba"
			fileExtensions = "moba"
			serializer = {
				generateStub = false
			}
			formatter = {
				generateStub=true
			}
			contentAssist = {
				generateStub=true
			}
			labelProvider ={
				generateStub=true
			}
			quickOutline = {
			}
			qualifiedNamesProvider = auto-inject{}
			
			validator = {
			// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
