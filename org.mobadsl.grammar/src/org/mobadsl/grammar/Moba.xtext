/**
 * Copyright (c) 2015 - 2016, Lunifera GmbH (Wien), Ekkehard Gentz (Rosenheim)
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *         Florian Pirchner - Initial implementation
 */
grammar org.mobadsl.^grammar.Moba hidden(WS, ML_COMMENT, SL_COMMENT)

import "platform:/resource/org.mobadsl.semantic.model/model/Moba.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

MobaModel:
	('copyright' copyright=STRING)?
	features+=MobaModelFeature*;

MobaModelFeature:
	MobaProject | MobaApplication;

MobaProject:
	'project' name=ID 'version' '=' version=VERSION MobaFriendsAble '{'
	(('uiApp' '=' uiApplication=[MobaApplication|ID])?
	&
	('backgroundApp' '=' backgroundApplication=[MobaApplication|ID])?)
	'}';

MobaApplication:
	'application' name=ID 'version' '=' version=VERSION ('cache' '=' defaultCache=[MobaCache])? MobaFriendsAble '{'
	(('javaPackage' javaPackage=FQN)? &
	(features+=MobaApplicationFeature*))
	'}';

MobaApplicationFeature:
	MobaDataType | MobaTemplate | MobaConstant | MobaSettings | MobaData | MobaREST | MobaGenerator | MobaAuthorization |
	MobaTransportSerializationType | MobaExternalModule | MobaTrigger |
	MobaGeneratorSlot | MobaServer | MobaPersistenceType | MobaCache;

MobaServer:
	'server' name=ID 'url' '=' (urlString=STRING | urlConst=[MobaConstant|CONSTANT]) ('extends' superType=[MobaServer])?
	MobaFriendsAble '{'
	(('authorization' '=' authorization=[MobaAuthorization|CONSTANT])? &
	('services' '=' '(' services+=[MobaREST] (',' services+=[MobaREST])* ')')?)
	'}';

	/**
 * Will import a Moba-Application.
 */
MobaTemplate:
	'use template' (template=[MobaApplication|ID] | downloadTemplate=DOWNLOAD_TEMPLATE);

MobaGenerator:
	(active?='active')? 'generator' name=ID '{'
	features+=MobaGeneratorFeature*
	'}';

MobaGeneratorFeature:
	MobaGeneratorIDFeature | MobaGeneratorMixinFeature;

MobaGeneratorIDFeature:
	'extensionPoint' generatorId=FQN ':' generatorVersion=VERSION;

MobaGeneratorMixinFeature:
	'mixin' generatorRef=[MobaGenerator|ID];

MobaAuthorization:
	'authorization' name=CONSTANT MobaFriendsAble;

MobaTransportSerializationType:
	'serialization' name=CONSTANT MobaFriendsAble;

MobaPersistenceType:
	'persistence' name=CONSTANT MobaFriendsAble;

MobaDataType:
	'datatype' name=ID ('extends' superType=[MobaDataType])?
	((array?='isArray')?
	& ('constraints' '(' constraints+=MobaConstraint (',' constraints+=MobaConstraint)* ')')?
	& (((date?='isDate' | time?='isTime' | timestamp?='isTimestamp') ('(' (dateFormatString=STRING |
	dateFormatConst=[MobaConstant|CONSTANT]) ')')?)
	| (enumAST=MobaEnum)
	| (predefined?='isPredefined')
	| (string?="isString" (primitive?='isPrimitive')?)
	| (bool?="isBool" (primitive?='isPrimitive')?)
	| (numeric?="isNumeric" (primitive?='isPrimitive')?)
	| (decimal?="isDecimal" (primitive?='isPrimitive')?))) MobaFriendsAble;

MobaEnum:
	{MobaEnum}
	'enum' '{'
	literals+=MobaEnumLiteral*
	'}';

MobaEnumLiteral:
	'lit' name=CONSTANT '=' '(' literal=STRING ',' value=INT ((',' default?='isDefault')? & (','
	undefined?='isUndefined')? & (','
	^hidden?='isHidden')?) ')';

MobaConstant:
	'const' name=CONSTANT '=' valueAST=MobaConstantValue MobaFriendsAble;

MobaConstantValue:
	(valueString=STRING | valueInt=INT | valueDouble=DOUBLE | (valueConst=[MobaConstant|CONSTANT] ('->'
	valueConstFunctions+=MobaConstantValueFunction)*)) ('+'
	tail=MobaConstantValue)?;

enum MobaConstantValueFunction:
	TO_FIRST_LOWER_CASE="toFirstLower" | TO_FIRST_UPPER_CASE="toFirstUpper" | TO_LOWER_CASE="toLowerCase" |
	TO_UPPER_CASE="toUpperCase";

fragment MobaFriendsAble:
	(('friends' '(' friends+=MobaFriend (',' friends+=MobaFriend)* ')')? & ('[' properties+=MobaProperty (','
	properties+=MobaProperty)* ']')?);

MobaFriend:
	(valueString=STRING | valueConst=[MobaConstant|CONSTANT]);

MobaProperty:
	(keyString=STRING | keyConst=[MobaConstant|CONSTANT]) '=' (valueString=STRING | valueConst=[MobaConstant|CONSTANT]);

MobaData:
	MobaEntity | MobaDto | MobaQueue;

MobaSettings:
	{MobaSettings}
	(active?='active')? 'settings' name=ID ('extends' superType=[MobaSettings])? MobaFriendsAble '{'
	features+=MobaSettingsFeature*
	'}';

MobaCache:
	{MobaCache}
	'cache' name=ID
	// optional
	('('
	// type
	(('type' '=' (cacheTypeString=STRING |
	cacheTypeConst=[MobaConstant|CONSTANT]))
	// strategy
	& ('strategy' '=' (cacheStrategyString=STRING |
	cacheStrategyConst=[MobaConstant|CONSTANT]))
	// strategy
	& ('interval' '=' (cacheIntervalInt=INT |
	cacheIntervalConst=[MobaConstant|CONSTANT]))?
	// strategy
	& ('using'
	cachePersistence=[MobaPersistenceType|CONSTANT])?) ')')?;

MobaEntity:
	'entity' name=ID (('extends' superType=[MobaEntity])? & ('cache' '=' cache=[MobaCache])?) MobaFriendsAble '{'
	features+=MobaEntityFeature*
	(indizes+=MobaEntityIndex)*
	'}';

MobaEntityIndex:
	(unique?='unique')? 'index' name=ID '(' attributes+=[MobaEntityAttribute|ID] (','
	attributes+=[MobaEntityAttribute|ID])* ')';

MobaDto:
	'dto' name=ID ('extends' superType=[MobaDto])? ('wraps' wrappedEntity=[MobaEntity])? MobaFriendsAble '{'
	features+=MobaDtoFeature*
	'}';

MobaQueue:
	'queue' name=ID ('extends' superType=[MobaQueue])? ('cache' '=' cache=[MobaCache])? MobaFriendsAble '{'
	features+=MobaQueueFeature*
	'}';

MobaREST:
	MobaRESTCustomService | MobaRESTCrud | MobaRESTWorkflow;

MobaRESTCustomService:
	'rest' (bigData?='bigData')? name=ID ('extends' superType=[MobaRESTCustomService])? MobaFriendsAble '{'
	(('headers' '{'
	headers+=MobaRestHeader*
	'}')? &
	('parameters' '{'
	parameters+=MobaRestParameter*
	'}')? &
	('autorization' '=' authorization=[MobaAuthorization|CONSTANT])?
	&
	('requestDto' '=' requestDto=MobaRESTPayloadDefinition)?
	&
	('responseDto' '=' responseDto=MobaRESTPayloadDefinition)?
	&
	('errorDto' '=' errorDto=MobaRESTPayloadDefinition)?
	&
	('method' '=' operation=MobaRESTMethods)?)
	'}';

MobaRESTPayloadDefinition:
	dto=[MobaDto] ((array?='isArray')? & ('as' serializationType=[MobaTransportSerializationType|CONSTANT])?);

MobaRestParameter returns MobaRESTAttribute:
	'param' type=[MobaDataType] (-> nameString=ID | nameConst=[MobaConstant|CONSTANT]) ('=' (valueString=STRING |
	valueInt=INT | valueDouble=DOUBLE | valueConst=[MobaConstant|CONSTANT]))?;

MobaRestHeader returns MobaRESTHeader:
	((contentTypeHeader?='ContentTypeHeader' (-> nameString=ID | nameConst=[MobaConstant|CONSTANT]))
	|
	(rawHeader?='RawHeader' (nameString=ID | nameConst=[MobaConstant|CONSTANT]) '=' (valueString=STRING |
	valueInt=INT | valueDouble=DOUBLE | valueConst=[MobaConstant|CONSTANT])));

MobaRESTCrud:
	'restCrud' name=ID ('extends' superType=[MobaRESTCrud])? MobaFriendsAble;

MobaRESTWorkflow:
	'restWorkflow' name=ID ('extends' superType=[MobaRESTWorkflow])? MobaFriendsAble '{'
	('service' services+=[MobaREST])*
	'}';

MobaEntityFeature:
	MobaEntityAttribute | MobaEntityReference | MobaEntityEmbeddable;

MobaEntityAttribute:
	'var' ((lazy?='lazy')? & (transient?='transient')? & (domainKey?='domainKey')? &
	(domainDescription?='domainDescription')?
	& ('constraints' '(' constraints+=MobaConstraint (','
	constraints+=MobaConstraint)* ')')?)
	type=[MobaDataType] MobaMultiplicityAble name=ID ('format' '(' (formatString=STRING |
	formatConst=[MobaConstant|CONSTANT]) ')')? MobaFriendsAble;

MobaSettingsFeature:
	MobaSettingsAttribute | MobaSettingsEntityReference;

MobaSettingsAttribute:
	'var' ((lazy?='lazy')? & (transient?='transient')? & (domainKey?='domainKey')? &
	(domainDescription?='domainDescription')?
	& ('constraints' '(' constraints+=MobaConstraint (','
	constraints+=MobaConstraint)* ')')?)
	type=[MobaDataType] MobaMultiplicityAble name=ID ('format' '(' (formatString=STRING |
	formatConst=[MobaConstant|CONSTANT]) ')')? MobaFriendsAble;

MobaSettingsEntityReference:
	'refEntity' ((cascading?='cascading')? & (lazy?='lazy')? & (transient?='transient')?) type=[MobaEntity]
	MobaMultiplicityAble
	name=ID
	MobaFriendsAble;

fragment MobaMultiplicityAble:
	(multiplicity=MobaMuliplicity)?;

MobaEntityReference:
	'ref' ((cascading?='cascading')? & (lazy?='lazy')? & (transient?='transient')?) type=[MobaEntity] MobaMultiplicityAble
	name=ID ('opposite' opposite=[MobaEntityReference])?
	MobaFriendsAble;

MobaEntityEmbeddable:
	'embed' (transient?='transient')? type=[MobaEntity] MobaMultiplicityAble
	name=ID
	MobaFriendsAble;

MobaDtoFeature:
	MobaDtoAttribute | MobaDtoReference | MobaDtoEmbeddable;

MobaDtoAttribute:
	'var' ((lazy?='lazy')? & (transient?='transient')? & (domainKey?='domainKey')? &
	(domainDescription?='domainDescription')?
	& ('constraints' '(' constraints+=MobaConstraint (','
	constraints+=MobaConstraint)* ')')?)
	type=[MobaDataType] MobaMultiplicityAble name=ID (('alias' '(' alias=ID ')')? & ('format' '(' (formatString=STRING |
	formatConst=[MobaConstant|CONSTANT]) ')')?) MobaFriendsAble;

MobaDtoReference:
	'ref' ((cascading?='cascading')? & (lazy?='lazy')? & (transient?='transient')?) type=[MobaDto]
	MobaMultiplicityAble
	name=ID ('alias' '(' alias=ID ')')? ('opposite' opposite=[MobaDtoReference])?
	MobaFriendsAble;

MobaDtoEmbeddable:
	'embed' (transient?='transient')? type=[MobaDto]
	MobaMultiplicityAble
	name=ID ('alias' '(' alias=ID ')')?
	MobaFriendsAble;

MobaQueueFeature:
	MobaQueueReference;

MobaQueueReference:
	'ref' ((cascading?='cascading')? & (lazy?='lazy')? & (transient?='transient')?) type=[MobaData] MobaMultiplicityAble
	name=ID
	MobaFriendsAble;

MobaMuliplicity:
	'[' (lower=MobaLowerBound) ('..' upper=MobaUpperBound)? ']';

MobaConstraint:
	MobaRegexpConstraint | MobaMinConstraint | MobaMaxConstraint | MobaMinLengthConstraint | MobaMaxLengthConstraint |
	MobaDigitsConstraint | MobaNullConstraint | MobaNotNullConstraint;

MobaRegexpConstraint:
	'regexp' '=' (filterString=STRING | filterConst=[MobaConstant|CONSTANT]);

MobaMinConstraint:
	'min' '=' (filterValue=DOUBLE | filterConst=[MobaConstant|CONSTANT]);

MobaMaxConstraint:
	'max' '=' (filterValue=DOUBLE | filterConst=[MobaConstant|CONSTANT]);

MobaMinLengthConstraint:
	'minLength' '=' (filterValue=INT | filterConst=[MobaConstant|CONSTANT]);

MobaMaxLengthConstraint:
	'maxLength' '=' (filterValue=INT | filterConst=[MobaConstant|CONSTANT]);

MobaDigitsConstraint:
	'digits' '(' (filterIntegerValue=INT | filterIntegerConst=[MobaConstant|CONSTANT]) ','
	(filterFractionValue=INT |
	filterFractionConst=[MobaConstant|CONSTANT]) ')';

MobaNullConstraint:
	{MobaNullConstraint}
	'isNull';

MobaNotNullConstraint:
	{MobaNotNullConstraint}
	'isNotNull';

MobaGeneratorSlot:
	'slot' name=ID ('type' type=STRING)?;

MobaTrigger:
	MobaAppInstallTrigger | MobaAppUpdateTrigger | MobaEmailTrigger | MobaDeviceStartupTrigger | MobaSMSTrigger |
	MobaPushTrigger | MobaGeofenceTrigger;

MobaAppInstallTrigger:
	'trigger app install' name=ID;

MobaAppUpdateTrigger:
	'trigger app update' name=ID;

MobaEmailTrigger:
	'trigger email' name=ID;

MobaDeviceStartupTrigger:
	'trigger device start' name=ID;

MobaSMSTrigger:
	'trigger sms' name=ID;

MobaPushTrigger:
	'trigger push' name=ID;

MobaGeofenceTrigger:
	'trigger geofence' name=ID 'event' '=' eventType=MobaGeofenceEvent;

enum MobaGeofenceEvent:
	ENTER="enter" | LEAVE="leave";

MobaExternalModule:
	MobaBluetoothModule | MobaPushModule | MobaNFCModule;

MobaBluetoothModule:
	'module bluetooth' name=ID 'type' '=' type=MobaBlueToothModuleType MobaFriendsAble;

enum MobaBlueToothModuleType:
	LE="lowEnergy" | SPP="spp" | BEACON="beacon";

MobaPushModule:
	'module push' name=ID MobaFriendsAble;

MobaNFCModule:
	'module NFC' name=ID 'type' '=' type=MobaNFCModuleType MobaFriendsAble;

enum MobaNFCModuleType:
	ID="id" | CUSTOM="custom" | TEXT='text';

enum MobaLowerBound:
	MANY='*' | ZERO='0' | OPTIONAL='?' | ATLEASTONE='+' | ONE='1';

enum MobaUpperBound:
	MANY='*' | ONE='1';

enum MobaRESTMethods:
	GET="GET" | POST="POST" | PUT="PUT" | DELETE="DELETE";

terminal CONSTANT:
	('A'..'Z') ('A'..'Z' | '_' | DIGIT)*;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal SIGN:
	('+' | '-');

terminal DIGIT:
	('0'..'9');

terminal DOUBLE returns ecore::EDouble:
	SIGN? DIGIT+ '.' DIGIT+;

terminal INT returns ecore::EInt:
	SIGN? DIGIT+;

terminal STRING:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' |
	"'" ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;

terminal VERSION:
	DIGIT+ '.' DIGIT+ '.' DIGIT+ ('-SNAPSHOT')?;

terminal FQN:
	ID ('.' ID)*;

terminal ID_VERSION:
	ID ":" VERSION;

terminal DOWNLOAD_TEMPLATE:
	'index://' ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '.' | '0'..'9')* ':' ID_VERSION;
